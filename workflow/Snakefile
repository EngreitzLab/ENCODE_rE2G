from snakemake.utils import min_version
min_version("7.0")

import pandas as pd


configfile: "config/config.yml"
conda: "mamba"

dataset_config_file = config["dataset_config"]
dataset_config = pd.read_table(dataset_config_file, na_values="").fillna("None").set_index("dataset", drop=False)

RESULTS_DIR = config["results_dir"]

rule all:
	input: 
		expand(os.path.join(RESULTS_DIR, "{dataset}", "NumCandidateEnhGene.txt"), dataset=dataset_config['dataset']),
		expand(os.path.join(RESULTS_DIR, "{dataset}",  "NumTSSEnhGene.txt"), dataset=dataset_config['dataset']),
		expand(os.path.join(RESULTS_DIR, "{dataset}", "NumEnhancersEG5kb.txt"), dataset=dataset_config['dataset']),
		expand(os.path.join(RESULTS_DIR, "{dataset}", "SumEnhancersEG5kb.txt"), dataset=dataset_config['dataset']),
		expand(os.path.join(RESULTS_DIR, "{dataset}", "ActivityOnly_features.tsv.gz"), dataset=dataset_config['dataset']),
		expand(
			os.path.join(RESULTS_DIR, "{dataset}", "EPCrisprBenchmark_ensemble_data_GRCh38.K562_ActivityOnly_features_{nafill}.tsv.gz"), nafill = ["withNA", "NAfilled"], dataset=dataset_config['dataset']
		)

# create DNase-only feature table
rule activity_only_features:
	input:
		feature_config = "config/feature_config.tsv",
		abc = lambda wildcards: dataset_config.loc[wildcards.dataset, "EnhancerPredictionsAllPutative"],
		NumCandidateEnhGene = os.path.join(RESULTS_DIR, "{dataset}", "NumCandidateEnhGene.txt"),
		NumTSSEnhGene = os.path.join(RESULTS_DIR, "{dataset}", "NumTSSEnhGene.txt"),
		NumEnhancersEG5kb = os.path.join(RESULTS_DIR, "{dataset}", "NumEnhancersEG5kb.txt"),
		SumEnhancersEG5kb = os.path.join(RESULTS_DIR, "{dataset}", "SumEnhancersEG5kb.txt"),
		ubiqExprGenes = config["ubiq_expr_genes"]
	params:
		activity = lambda wildcards: dataset_config.loc[wildcards.dataset, "activity"]
	output: 
		dnase_feature_tsv = os.path.join(RESULTS_DIR, "{dataset}", "ActivityOnly_features.tsv.gz")
	conda:
		"envs/encode_e2g_features.yml"
	script:
		"scripts/dnase_only_features.R"

# overlap DNase-only feature table table with K562 CRISPR data
rule overlap_activity_only_features_crispr:
	input:
		features = os.path.join(RESULTS_DIR, "{dataset}/ActivityOnly_features.tsv.gz"),
		crispr = config['crispr_dataset'],
		config = config['feature_config'],
		tss = config['gene_TSS500']
	output: 
		os.path.join(RESULTS_DIR, "{dataset}", "EPCrisprBenchmark_ensemble_data_GRCh38.K562_ActivityOnly_features_{nafill}.tsv.gz")
	params:
		filter_genes = "none",
		activity = lambda wildcards: dataset_config.loc[wildcards.dataset, "activity"]
	conda:
		"envs/encode_e2g_features.yml" 
	script:
		"scripts/overlap_features_with_crispr_data.R"


rule gen_new_features: 
	input:
		abc_predictions = lambda wildcards: dataset_config.loc[wildcards.dataset, "EnhancerPredictionsAllPutative"],
		enhancer_list = lambda wildcards: dataset_config.loc[wildcards.dataset, "EnhancerList"]
	params:
		gene_TSS500 = config['gene_TSS500'],
		chr_sizes = config['chr_sizes']
	conda:
		"envs/encode_e2g_features.yml"
	output: 
		NumCandidateEnhGene = os.path.join(RESULTS_DIR, "{dataset}", "NumCandidateEnhGene.txt"),
		NumTSSEnhGene = os.path.join(RESULTS_DIR, "{dataset}", "NumTSSEnhGene.txt"),
		NumEnhancersEG5kb = os.path.join(RESULTS_DIR, "{dataset}", "NumEnhancersEG5kb.txt"),
		SumEnhancersEG5kb = os.path.join(RESULTS_DIR, "{dataset}", "SumEnhancersEG5kb.txt"),
		NumEnhancersEG10kb = os.path.join(RESULTS_DIR, "{dataset}", "NumEnhancersEG10kb.txt"),
		SumEnhancersEG10kb = os.path.join(RESULTS_DIR, "{dataset}", "SumEnhancersEG10kb.txt"),
	shell: 
		""" 
		python workflow/scripts/gen_new_features.py \
			--enhancer_list {input.enhancer_list} \
			--abc_predictions {input.abc_predictions} \
			--ref_gene_tss {params.gene_TSS500} \
			--chr_sizes {params.chr_sizes} \
			--results_dir {RESULTS_DIR}{wildcards.dataset}
		"""